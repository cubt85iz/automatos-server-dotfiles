set ignore-comments := true

# Recipes for container management
import ".just/containers.just"

# Recipes for dotfiles management
import ".just/dotfiles.just"

# Recipes for firewall operations
import ".just/firewall.just"

# Recipes for logging operations
import ".just/log.just"

# Recipes for network operations
import ".just/network.just"

# Recipes for shell operations
import ".just/shell.just"

# Recipes for systemd operations
import ".just/systemd.just"

# Recipes for utilities
import ".just/utilities.just"

# Recipes for ZFS operations
import ".just/zfs.just"

# NOTE: Use this file to store shared recipes that can be executed from
# any imported recipe.

[private]
default:
  @just -l

# Use process substitution to use these colors in recipes.
# source <(just get-colors)
[private]
get-colors:
  #!/usr/bin/env bash

  cat <<EOF
  NC='\033[0m'
  BLACK='\033[0;30m'
  RED='\033[0;31m'
  GREEN='\033[0;32m'
  ORANGE='\033[0;33m'
  BLUE='\033[0;34m'
  PURPLE='\033[0;35m'
  CYAN='\033[0;36m'
  LIGHT_GRAY='\033[0;37m'
  DARK_GRAY='\033[1;30m'
  LIGHT_RED='\033[1;31m'
  LIGHT_GREEN='\033[1;32m'
  YELLOW='\033[1;33m'
  LIGHT_BLUE='\033[1;34m'
  LIGHT_PURPLE='\033[1;35m'
  LIGHT_CYAN='\033[1;36m'
  WHITE='\033[1;37m'
  EOF

# Outputs provided message to stderr in standardized format
[private]
stderr message level="ERROR":
  @>&2 echo "[{{ level }}] {{ message }}"

# Outputs provided message to stdout in standardized format
[private]
stdout message level="INFO":
  #! /usr/bin/env bash

  set -euo pipefail

  LEVEL="{{ level }}"
  MESSAGE="{{ message }}"

  if [ "$LEVEL" != "ERROR" ]; then
    echo -e "[{{ level }}] {{ message }}"
  else
    just stderr "$MESSAGE"
  fi

# Determines if the specified file exists.
[private]
file_exists file:
  #!/usr/bin/env bash

  set -euox pipefail

  FILE={{ file }}

  if [ ! -e "$FILE" ]; then
    just stderr "Provided file ($FILE) does not exist."
    exit 1
  fi
